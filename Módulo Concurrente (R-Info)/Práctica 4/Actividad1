{Existe un robot que sirve de flores a tres robots clientes. Cada cliente solicita al servidor que le deposite en su esquina siguiente una cantidad de flores aleatoria (entre 1 y 4). Por ejemplo, si el cliente se encuentra en la esquina (2,1) le solicitara que coloque x cantidad de flores en la esquina (2,2). Cuando el robot servidor deposita las flores en la esquina solicitada, el cliente las junta y las deposita una a una a lo largo de la avenida en la que se encuentra. El programa finaliza cuando todos los robos clientes completan su avenida. Asuma que el robot servidor tiene flores suficientes en su bolsa. El robot servidor se inicia en la esquina (100,100) Los robots clientes inician en las esquinas (1,1) , (2,1) y (3,1) respectivamente}
programa Actividad1
procesos
  proceso irEsquina (E av : numero; E ca : numero; E flores : numero)
  comenzar
    Pos(av,ca+1)
    repetir flores
      depositarFlor
    Pos(100,100)
  fin
areas
  areaServer: AreaP (100,100,100,100)
  av1: AreaPC(1,1,1,100)
  av2: AreaPC(2,1,2,100)
  av3: AreaPC(3,1,3,100)
robots
  robot cliente
  variables
    miCalle,miAvenida,floresPedidas : numero
    termino,seguir : boolean
  comenzar
    miAvenida := PosAv
    seguir := V
    mientras(seguir)
      miCalle := PosCa
      EnviarMensaje(miAvenida,R_Server)
      EnviarMensaje(miCalle, R_Server)
      Random(floresPedidas,1,4)
      si (miCalle = 100)
        floresPedidas:= 0
        seguir:=F
      EnviarMensaje(floresPedidas,R_Server)
      RecibirMensaje(termino,R_Server)
      si (seguir)
        mover
        repetir floresPedidas
          tomarFlor
        mientras((PosCa<100) & (floresPedidas>0))
          depositarFlor
          mover
          floresPedidas := floresPedidas - 1
        si (floresPedidas>0)
          depositarFlor
  fin
  robot servidor
  variables
    ID,ca,flores : numero
    seguir1,seguir2,seguir3 : boolean
  comenzar
    seguir1 := V
    seguir2 := V
    seguir3 := V
    mientras ((seguir1)|(seguir2)|(seguir3))
      RecibirMensaje(ID,*)
      si (ID = 1)
        RecibirMensaje(ca,r1)
        RecibirMensaje(flores,r1)
        si (flores = 0)
          seguir1:=F
        sino
          irEsquina(ID,ca,flores)
        EnviarMensaje(V,r1)
      sino
        si (ID = 2)
          RecibirMensaje(ca,r2)
          RecibirMensaje(flores,r2)
          si (flores = 0)
            seguir2:=F
          sino
            irEsquina(ID,ca,flores)
          EnviarMensaje(V,r2)
        sino
          RecibirMensaje(ca,r3)
          RecibirMensaje(flores,r3)
          si (flores = 0)
            seguir3:=F
          sino
            irEsquina(ID,ca,flores)
          EnviarMensaje(V,r3)
  fin
variables
  r1,r2,r3 : cliente
  R_Server: servidor
comenzar
  AsignarArea(R_Server, areaServer)
  AsignarArea(r1,av1)
  AsignarArea(r2,av2)
  AsignarArea(r3,av3)
  AsignarArea(R_Server,av1)
  AsignarArea(R_Server,av2)
  AsignarArea(R_Server,av3)
  Iniciar(R_Server, 100,100)
  Iniciar(r1,1,1)
  Iniciar(r2,2,1)
  Iniciar(r3,3,1)
fin
